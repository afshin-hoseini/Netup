apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


version = "0.2.3"

android {
    compileSdkVersion 23
    buildToolsVersion '25.0.0'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 7
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def siteUrl = 'https://github.com/afshin-hoseini/Netup'   // Homepage URL of the library
def gitUrl = 'https://github.com/afshin-hoseini/Netup.git'   // Git repository URL
group = "ir.afshin.netup"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name 'Netup'
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The MIT Software License'
                        url 'https://tldrlegal.com/license/mit-license'
                    }
                }
                developers {
                    developer {
                        id 'afshin-hoseini'
                        name 'Afshin'
                        email 'afshin.hoseini@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    //options.encoding = 'UTF-8'
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {

    def jsonFile = file('../Keys/keys.json')
    def jsKeys = new groovy.json.JsonSlurper().parseText(jsonFile.text)

    user = jsKeys.bintray.user
    key = jsKeys.bintray.apiKey

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "Netup"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}